# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch out hard ARM march and float specification

pkgname=openlibm
pkgver=0.8.1
pkgrel=1
pkgdesc='Standalone implementation of C mathematical functions'
arch=(x86_64)
url='https://openlibm.org/'
license=(BSD)
depends=(glibc)
source=(https://github.com/JuliaLang/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        '0001-Remove-ARM-march-and-float-specification.patch')
sha256sums=('ba8a282ecd92d0033f5656bb20dfc6ea3fb83f90ba69291ac8f7beba42dcffcf'
            '2b16d1e852a88bc6d31e9b25ed60b1a374f46b2034ce3449c2b8dafc6d2007c9')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-Remove-ARM-march-and-float-specification.patch
  sed -e 's|/usr/local|/usr|' -i Make.inc
}

build() {
  cd $pkgname-$pkgver
  make prefix=/usr
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
