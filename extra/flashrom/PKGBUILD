# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: fill <mailbox@fillo.ru>
# Contributor: Anton Shestakov <engored*ya.ru>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -Dpciutils=false - PCI isn't supported on ARM in this tool
#  - upstream patch to fix aarch64 FTBFS

pkgname=flashrom
pkgver=1.2
pkgrel=4
pkgdesc="Utility for reading, writing, erasing and verifying flash ROM chips"
arch=(x86_64)
url="https://www.flashrom.org/"
license=(GPL)
depends=(libftdi pciutils libusb)
makedepends=(meson)
optdepends=('dmidecode: for SMBIOS/DMI table decoder support')
source=("https://download.flashrom.org/releases/${pkgname}-v${pkgver}.tar.bz2"{,.asc}
        https://github.com/flashrom/flashrom/commit/da6b3b70cb852dd8e9f9e21aef95fa83e7f7ab0d.patch)
sha256sums=('e1f8d95881f5a4365dfe58776ce821dfcee0f138f75d0f44f8a3cd032d9ea42b'
            'SKIP'
            '074c17d617f95b5af01023ecb132db4b5061f5c5019127b61f9a41d2f7b58f5e')
validpgpkeys=(58A4868B25C7CFD662FB0132A3EB95B8D9780F68) # David Hendricks (packaging key) <david.hendricks@gmail.com>

prepare() {
  cd ${pkgname}-v${pkgver}
  patch -p1 -i ../da6b3b70cb852dd8e9f9e21aef95fa83e7f7ab0d.patch
}

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson ../build -Dpciutils=false
  ninja -v -C ../build
  # Workaround for meson not installing manpage
  make
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  # Workaround for meson not installing manpage
  cd ${pkgname}-v${pkgver}
  make PREFIX=temp/ install
  cp -r temp/share "${pkgdir}"/usr/
}
