# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

pkgname=mkinitcpio
pkgver=36
pkgrel=1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
checkdepends=('bash-bats' 'bash-bats-assert' 'lzop')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch')
install=mkinitcpio.install
sha512sums=('f13cfdfee62dc1a344b75413fde8f35eb594c4c372d4a2ed8bbc22f2c01d93ea59d423807d06d19a6d1789e47b35286845daffeffef0fec4bae022e0e92b7b64'
            'SKIP'
            '606ddec26b7b70976f3892cd195d1feb5eee9b79867573bff40a3aa3614b84de85b2cd8abc6929eda8c420930407bc3aa0cf4edc13891ab1a74c845e0c435b9f'
            'e4f4eceefa20716631a2bb5bed02b0a7484c39c49e977ebc6d3b8ee0c7a4228e48ad94fd85aa6adb32958bbc57ab5844dafaf15c9b5e016deb97c2a9e0cfbe1e'
            'f8743e9e6da85c77a89bf8bc28af44d8b40997d3d1f0ce7735e31e323bb3c9d156811c28a045322acaa91befdeed8c6ce281c2659dc0c6ab81c73c674e2ef6a2')
b2sums=('b60d8e61a15167df3316a7336467740efd4888784228dd6a08b1d974c54c479c082ba142eb60d3f356b06053e5c472f747e0ca830d50bed9f31c13d52b549ca1'
        'SKIP'
        'be971d8a9d194a35907afadc24bc5cb9b0b2434c264fa595c73c828912c694d5dc5c3129f2da8e74a38fd5439b9bcdc6a49c8ee9873bcd3f8e9c5af2a1269eb0'
        '5c28cf1c0a7492736c0a074f5fc085e581b219b25fe6bdb81a3d8f07cbc0e334d76a2565b32cb841724550671d62429fa950ae6ce472420f6fe0c2049f61a753'
        '6a5fe2da925f9f5327e14cb339226ed36ac416a00b7b6b09d4c9d332ddaf8f982921d1ea3f2af0d17e4add7a0cc4a253047265ddcc3e98a707b13f28bf294775')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
